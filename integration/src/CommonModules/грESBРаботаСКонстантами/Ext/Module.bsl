
#Область ПрограммныйИнтерфейс

// Функция - Получить длительность хранения
// 
// Возвращаемое значение:
//	Значение константы "грESBДлительностьХраненияВходящихСообщенийПоОчередямИСостояниям".
//
Функция ПолучитьДлительностьХранения() Экспорт
	
	ЗначениеКонстанты = Константы.грESBДлительностьХраненияВходящихСообщенийПоОчередямИСостояниям.Получить().Получить();
	
	Если ЗначениеКонстанты = Неопределено Тогда
		 
		Возврат СформироватьЗначениеКонстантыДлительностьХранения();
		
	Иначе
		
		Возврат ЗначениеКонстанты;
		
	КонецЕсли;
	
КонецФункции

// Процедура - Устанавливает константу грESBДлительностьХраненияВходящихСообщенийПоОчередямИСостояниям
//
// Параметры:
//  ТаблицаНастроек - Таблица значений с настройками
//
Процедура УстановитьДлительностьХранения(ТаблицаНастроек) Экспорт 
	
	ХранилищеТаблицыНастроек = Новый ХранилищеЗначения(ТаблицаНастроек);
	Константы.грESBДлительностьХраненияВходящихСообщенийПоОчередямИСостояниям.Установить(ХранилищеТаблицыНастроек);
	
КонецПроцедуры

// Функция - Сформировать значение константы Длительность хранения
//
// Возвращаемое значение:
//	Пустая таблица значений для описания константы Длительность хранения.
//
Функция СформироватьЗначениеКонстантыДлительностьХранения() Экспорт
	 
	ТаблицаДлительностиОжидания = Новый ТаблицаЗначений;
	ТаблицаДлительностиОжидания.Колонки.Добавить("СтатусСообщения", Новый ОписаниеТипов("ПеречислениеСсылка.сшпСтатусыСообщений"), "Статус сообщения");
	ТаблицаДлительностиОжидания.Колонки.Добавить("ДлительностьХранения", Новый ОписаниеТипов("Число"), "Длительность хранения, дн.");
	
	Возврат ТаблицаДлительностиОжидания;
	
КонецФункции

// Функция - Получить длительность хранения по умолчанию
// 
// Возвращаемое значение:
//	Значение константы "грESBДлительностьХраненияВходящихСообщений".
//
Функция ПолучитьДлительностьХраненияПоУмолчанию() Экспорт
	
	Возврат Константы.грESBДлительностьХраненияВходящихСообщений.Получить();
	
КонецФункции

// Функция - Проверить заполнение зарезервированных потоков
// 
// Возвращаемое значение:
//	Структура - ОбщееКоличествоПотоков, ОбщееКоличествоЗарезервированныхПотоков, ОбщееКоличествоЗарезервированныхПотоковПоОчередям, Ошибка
//
Функция ПроверитьЗаполнениеЗарезервированныхПотоков(ИзменениеКоличестваЗарезервированныхПотоков = 0) Экспорт 
	
	ОбщееКоличествоПотоков = Константы.грESBКоличествоПотоковОбработкиОчередей.Получить();
	ОбщееКоличествоЗарезервированныхПотоков = Константы.грESBКоличествоЗарезервированныхПотоковОбработкиОчередей.Получить(); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(грВходящиеВнутренниеОчереди.КоличествоЗарезервированныхПотоков) КАК КоличествоЗарезервированныхПотоков
	               |ИЗ
	               |	Справочник.грВходящиеВнутренниеОчереди КАК грВходящиеВнутренниеОчереди";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ОбщееКоличествоЗарезервированныхПотоковПоОчередям = Выборка.КоличествоЗарезервированныхПотоков;
	Иначе
		ОбщееКоличествоЗарезервированныхПотоковПоОчередям = 0;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ОбщееКоличествоПотоков", ОбщееКоличествоПотоков);
	СтруктураВозврата.Вставить("ОбщееКоличествоЗарезервированныхПотоков", ОбщееКоличествоЗарезервированныхПотоков);
	СтруктураВозврата.Вставить("ОбщееКоличествоЗарезервированныхПотоковПоОчередям", ОбщееКоличествоЗарезервированныхПотоковПоОчередям);
	
	Если ОбщееКоличествоПотоков - ОбщееКоличествоЗарезервированныхПотоков - ОбщееКоличествоЗарезервированныхПотоковПоОчередям - ИзменениеКоличестваЗарезервированныхПотоков <= 0 Тогда 
		СтруктураВозврата.Вставить("Ошибка", Истина);
	Иначе
		СтруктураВозврата.Вставить("Ошибка", Ложь);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции
	
#КонецОбласти