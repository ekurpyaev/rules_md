#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбновитьТаблицуРегламентныхЗаданийНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_РегламентныеЗадания" Тогда
		ОбновитьТаблицуРегламентныхЗаданийНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаРегламентныеЗадания

&НаКлиенте
Процедура ТаблицаРегламентныеЗаданияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ДобавитьСкопироватьИзменитьРегламентноеЗадание("Изменить");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	ДобавитьСкопироватьИзменитьРегламентноеЗадание("Добавить");
КонецПроцедуры

&НаКлиенте
Процедура Скопировать(Команда)
	ДобавитьСкопироватьИзменитьРегламентноеЗадание("Скопировать");
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	ДобавитьСкопироватьИзменитьРегламентноеЗадание("Изменить");
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	Если Элементы.ТаблицаРегламентныеЗадания.ВыделенныеСтроки.Количество() > 1 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите одно регламентное задание.'"));
		
	ИначеЕсли Элементы.ТаблицаРегламентныеЗадания.ТекущиеДанные.Предопределенное Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Невозможно удалить предопределенное регламентное задание.'") );
	Иначе
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ТаблицаРегламентныеЗаданияПередУдалениемЗавершение", ЭтотОбъект),
			НСтр("ru = 'Удалить регламентное задание?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьТаблицуРегламентныхЗаданийНаСервере()

	Отбор = Новый Структура("Метаданные", Метаданные.РегламентныеЗадания.грОтложенныеОперацииИнтеграции);
	ТекущиеЗадания = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Отбор);

	ТаблицаРегламентныеЗадания.Очистить();
	
	Для Каждого ЭлементРегЗадание Из ТекущиеЗадания Цикл
		НоваяСтрока = ТаблицаРегламентныеЗадания.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементРегЗадание);
		НоваяСтрока.Идентификатор = Строка(ЭлементРегЗадание.УникальныйИдентификатор);
	КонецЦикла;

КонецПроцедуры // ОбновитьТаблицуРегламентныхЗаданийНаСервере()

&НаКлиенте
Процедура ТаблицаРегламентныеЗаданияПередУдалениемЗавершение(Ответ, Контекст) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьРегламентноеЗаданиеВыполнитьНаСервере(
			Элементы.ТаблицаРегламентныеЗадания.ТекущиеДанные.Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьРегламентноеЗаданиеВыполнитьНаСервере(Знач Идентификатор)
	
	Задание = РегламентныеЗаданияСервер.ПолучитьРегламентноеЗадание(Идентификатор);
	Строка = ТаблицаРегламентныеЗадания.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор))[0];
	Задание.Удалить();
	ТаблицаРегламентныеЗадания.Удалить(ТаблицаРегламентныеЗадания.Индекс(Строка));
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСкопироватьИзменитьРегламентноеЗадание(Знач Действие)
	
	Если Элементы.ТаблицаРегламентныеЗадания.ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите регламентное задание.'"));
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Идентификатор",	Элементы.ТаблицаРегламентныеЗадания.ТекущиеДанные.Идентификатор);
		ПараметрыФормы.Вставить("Действие",			Действие);
		
		ОткрытьФорму("Обработка.грУправлениеОтложеннымиОперациямиИнтеграции.Форма.РегламентноеЗадание", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
