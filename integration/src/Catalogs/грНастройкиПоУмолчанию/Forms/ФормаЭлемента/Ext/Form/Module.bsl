
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьВидимостьЭлементовФормы();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Описание = Справочники.грНастройкиПоУмолчанию.ПолучитьОписаниеНастройки(Объект.Ссылка);
	КонецЕсли;
	
	Элементы.Описание.Видимость = ЗначениеЗаполнено(Описание)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗначениеСпискомПриИзменении(Элемент)
	
	ОбновитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Ссылка = ПредопределенноеЗначение("Справочник.грНастройкиПоУмолчанию.ПомещениеПоставщика") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Отбор = Новый Структура;
		Отбор.Вставить("грЛогическийСклад",ПредопределенноеЗначение("Перечисление.грЛогическиеСклады.СкладПоставщика"));
		
		ПараметрыСкладов = Новый Структура("Отбор");
		ПараметрыСкладов.Отбор = Отбор;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборСкладаЗавершение", ЭтотОбъект);
		ОткрытьФорму("Справочник.Склады.Форма.ФормаВыбора", ПараметрыСкладов, ЭтотОбъект,,,,ОписаниеОповещения);
	//+++ GRADUM, Теплоухов А.В. 2021_10_05 Задача № ERP-1398{
	//ИначеЕсли Объект.Ссылка = ПредопределенноеЗначение("Справочник.грНастройкиПоУмолчанию.ПомещениеПолучательРО") Тогда
	ИначеЕсли Объект.Ссылка = ПредопределенноеЗначение("Справочник.грНастройкиПоУмолчанию.ПомещениеПолучательРО")
		Или Объект.Ссылка = ПредопределенноеЗначение("Справочник.грНастройкиПоУмолчанию.ПомещениеПоставщикаОклейкаУПоставщика")
		Или Объект.Ссылка = ПредопределенноеЗначение("Справочник.грНастройкиПоУмолчанию.ЗагрузкаЗПИзExcelПомещениеПоставщика")
		Тогда
	//--- GRADUM, Теплоухов А.В. 2021_10_05 Задача № ERP-1398}
		
		СтандартнаяОбработка = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборСкладаЗавершение", ЭтотОбъект);
		ОткрытьФорму("Справочник.Склады.Форма.ФормаВыбора",, ЭтотОбъект,,,, ОписаниеОповещения);
		
	ИначеЕсли Объект.Ссылка = ПредопределенноеЗначение("Справочник.грНастройкиПоУмолчанию.ЗагрузкаЗПИзExcelБанковскийСчет") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборБанковскогоСчетаЗавершение", ЭтотОбъект);
		ОткрытьФорму("Справочник.Организации.Форма.ФормаВыбора",, ЭтотОбъект,,,, ОписаниеОповещения);
		
	ИначеЕсли Объект.Ссылка = ПредопределенноеЗначение("Справочник.грНастройкиПоУмолчанию.АкцияДляПрограммыЛояльности") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Группы);
		
		ОткрытьФорму("Справочник.СкидкиНаценки.ФормаВыбораГруппы", ПараметрыФормы, Элемент, ,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОповещенияФормы

&НаКлиенте
Процедура ВыборСкладаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.Склады") Тогда
		
		ПараметрыФормы = Новый Структура("Отбор");
		Отбор = Новый Структура;
		Отбор.Вставить("Владелец",Результат);
		ПараметрыФормы.Отбор = Отбор;
		ОткрытьФорму("Справочник.СкладскиеПомещения.Форма.ФормаВыбора", ПараметрыФормы, Элементы.Значение,,,);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборБанковскогоСчетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.Организации") Тогда
		
		Отбор = Новый Структура;
		Отбор.Вставить("Владелец",Результат);
		
		ПараметрыФормы = Новый Структура("Отбор");
		ПараметрыФормы.Отбор = Отбор;
		
		ОткрытьФорму("Справочник.БанковскиеСчетаОрганизаций.Форма.ФормаВыбора", ПараметрыФормы, Элементы.Значение,,,);
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьВидимостьЭлементовФормы()
	
	Элементы.Значение.Видимость = НЕ Объект.ЗначениеСписком;
	Элементы.Значения.Видимость = Объект.ЗначениеСписком;
		
КонецПроцедуры

#КонецОбласти  //СлужебныеПроцедурыИФункции
 