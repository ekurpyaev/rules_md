#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблКлассов.НомерСтроки КАК НомерСтроки,
	               |	ТаблКлассов.Класс КАК Класс
	               |ПОМЕСТИТЬ ВТТаблКлассов
	               |ИЗ
	               |	&ТаблКлассов КАК ТаблКлассов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТТаблКлассов.НомерСтроки) КАК НомерСтроки,
	               |	ВТТаблКлассов.Класс КАК Класс
	               |ИЗ
	               |	ВТТаблКлассов КАК ВТТаблКлассов
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТТаблКлассов.Класс
	               |
	               |ИМЕЮЩИЕ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТТаблКлассов.НомерСтроки) > 1";
	Запрос.УстановитьПараметр("ТаблКлассов", ОбрабатываемыеКлассы.Выгрузить());
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Отказ = Истина;
		воОбщегоНазначенияВызовСервера.СообщитьПользователю(НСтр("ru = 'Обнаружены дубли классов. Запись невозможна.'"), Ссылка);
	КонецЕсли;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	воВходящиеВнутренниеОчереди.КоличествоЗарезервированныхПотоков КАК КоличествоЗарезервированныхПотоков
	               |ИЗ
	               |	Справочник.воВходящиеВнутренниеОчереди КАК воВходящиеВнутренниеОчереди
	               |ГДЕ
	               |	воВходящиеВнутренниеОчереди.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КоличествоЗарезервированныхПотоковБыло = Выборка.КоличествоЗарезервированныхПотоков;	
	Иначе
		КоличествоЗарезервированныхПотоковБыло = 0;	
	КонецЕсли;
	
	КоличествоИзмененных = КоличествоЗарезервированныхПотоков - КоличествоЗарезервированныхПотоковБыло;
	
	СтруктураВерности = воРаботаСКонстантамиПолныеПраваВызовСервера.ПроверитьЗаполнениеЗарезервированныхПотоков(КоличествоИзмененных);
	Если СтруктураВерности.Ошибка Тогда
		Отказ = Истина;
		воОбщегоНазначенияВызовСервера.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Ошибка при заполнении элемента Справочника.
                                                                          |Общее количество потоков: %1 меньше или зарезервированным  потокам. Общее: %2, для очередей: %3.'")
		, Формат(СтруктураВерности.ОбщееКоличествоПотоков, "ЧДЦ=0; ЧН=0")
		, Формат(СтруктураВерности.ОбщееКоличествоЗарезервированныхПотоков, "ЧДЦ=0; ЧН=0")
		, Формат(СтруктураВерности.ОбщееКоличествоЗарезервированныхПотоковПоОчередям + КоличествоИзмененных, "ЧДЦ=0; ЧН=0")));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли