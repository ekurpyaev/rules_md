
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ПривилегированныйРежим() Тогда
		ВыполнитьПроверкуПравДоступа("Администрирование", Метаданные);
	КонецЕсли;
	
	ЗаполнитьФормуНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьФорму(Команда)
	
	ЗаполнитьФормуНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФормуНаСервере()
	
	ТаблицаОбработчиков.Очистить();
	
	ЗапросКоличестваСообщений = Новый Запрос;
	ЗапросКоличестваСообщений.Текст = "ВЫБРАТЬ
	                                  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ воВходящиеСообщенияВОбработке.ИдентификаторСообщения) КАК КоличествоВОбработке
	                                  |ИЗ
	                                  |	РегистрСведений.воВходящиеСообщенияВОбработке КАК воВходящиеСообщенияВОбработке
	                                  |ГДЕ
	                                  |	воВходящиеСообщенияВОбработке.КлючЗадания = &КлючЗадания";
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяМетода", "воВходящиеОчередиВызовСервера.ОбработатьВходящиеСообщения");
	СтруктураОтбора.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	Обработчики = ФоновыеЗадания.ПолучитьФоновыеЗадания(СтруктураОтбора);
	
	Для Каждого Обработчик Из Обработчики Цикл
		СтрокаКонсоли = ТаблицаОбработчиков.Добавить();
		Если СтрНачинаетсяС(Обработчик.Ключ, "ОбщийОбработчик") Тогда
			СтрокаКонсоли.ВидОбработчика = НСтр("ru = 'Общий обработчик'");
		ИначеЕсли СтрНачинаетсяС(Обработчик.Ключ, "ОбщийПриоритет") Тогда
			СтрокаКонсоли.ВидОбработчика = НСтр("ru = 'Общий приоритет'");
		ИначеЕсли СтрНачинаетсяС(Обработчик.Ключ, "ОбщийНагрузка") Тогда
			СтрокаКонсоли.ВидОбработчика = НСтр("ru = 'Общий нагрузка'");
		ИначеЕсли СтрНачинаетсяС(Обработчик.Ключ, "РезервныйОчередь") Тогда
			СтрокаКонсоли.ВидОбработчика = НСтр("ru = 'Резервный очередь'");
		Иначе	
			СтрокаКонсоли.ВидОбработчика = "";
		КонецЕсли;
		УИД = Новый УникальныйИдентификатор(Прав(Обработчик.Ключ, 36));
		Очередь = Справочники.воВходящиеВнутренниеОчереди.ПолучитьСсылку(УИД);
		Если Очередь = Справочники.воВходящиеВнутренниеОчереди.ПустаяСсылка() Тогда
			СтрокаКонсоли.Наименование = НСтр("ru = 'Общая очередь'");
			СтрокаКонсоли.Отключена = Ложь;
		Иначе
			СтрокаКонсоли.Наименование = Очередь.Наименование;
			СтрокаКонсоли.Отключена = Очередь.ОчередьОтключена;
		КонецЕсли;
		СтрокаКонсоли.Ссылка = Очередь;
		СтрокаКонсоли.НачалоВыполнения = Обработчик.Начало;
		СтрокаКонсоли.Выполняется = Истина;
		ЗапросКоличестваСообщений.УстановитьПараметр("КлючЗадания", Обработчик.Ключ);
		ВыборкаКоличества = ЗапросКоличестваСообщений.Выполнить().Выбрать();
		Если ВыборкаКоличества.Следующий() Тогда
			СтрокаКонсоли.КоличествоСообщенийВОбработке = ВыборкаКоличества.КоличествоВОбработке;
		Иначе
			СтрокаКонсоли.КоличествоСообщенийВОбработке = 0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
