#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура БыстрыйПоиск(Команда)
	КлючЗаписи = ПолучитьКлючЗаписиПоСтрокеПоиска(СтрокаБыстрогоПоиска);
	Если ТипЗнч(КлючЗаписи) = Тип("Строка") Тогда 
		ПоказатьОповещениеПользователя(КлючЗаписи);
	Иначе
		Элементы.Список.ТекущаяСтрока = КлючЗаписи;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВернутьВОбработку(Команда)
	
	ВернутьВОбработкуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОбработку(Команда)
	
	ОтменитьОбработкуНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
// Процедура - Вернуть в обработку на сервере
//
Процедура ВернутьВОбработкуНаСервере()
	
	Для Каждого ключЗаписи Из Элементы.Список.ВыделенныеСтроки Цикл 
		
		воВходящиеОчередиВызовСервера.УстановитьСостояниеСообщения(ключЗаписи.ИдентификаторСообщения, Перечисления.воСтатусыСообщений.ОжиданиеОбработки); 
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
// Процедура - Отменить обработку на сервере
//
Процедура ОтменитьОбработкуНаСервере()
	
	Для Каждого ключЗаписи Из Элементы.Список.ВыделенныеСтроки Цикл 
		
		воВходящиеОчередиВызовСервера.УстановитьСостояниеСообщения(ключЗаписи.ИдентификаторСообщения, Перечисления.воСтатусыСообщений.ОбработкаОтменена); 
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
// Функция - ПолучитьКлючЗаписиПоСтрокеПоиска
// 
// Возвращаемое значение:
// КлючЗаписи, Строка - ключ записи - если запись найдена, строка - текст ошибки.
//
Функция ПолучитьКлючЗаписиПоСтрокеПоиска(СтрокаПоиска) 
	
	Попытка
		
		ИдентификаторСообщения = Новый УникальныйИдентификатор(СтрокаПоиска);
	
	Исключение
		
		Возврат НСтр("ru = 'Для поиска требуется ввести в строке уникальный идентификатор сообщения полностью'");
	
	КонецПопытки;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	воОчередьВходящихСообщений.ИдентификаторСообщения
		|ИЗ
		|	РегистрСведений.воОчередьВходящихСообщений КАК воОчередьВходящихСообщений
		|ГДЕ
		|	воОчередьВходящихСообщений.ИдентификаторСообщения = &ИдентификаторСообщения";	
	Запрос.УстановитьПараметр("ИдентификаторСообщения", ИдентификаторСообщения);
	
	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		 
		Возврат НСтр("ru = 'Сообщение не найдено'");
		
	Иначе
		
		Возврат РегистрыСведений.воОчередьВходящихСообщений.СоздатьКлючЗаписи(Новый Структура("ИдентификаторСообщения", ИдентификаторСообщения));
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти